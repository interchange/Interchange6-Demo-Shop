#! /usr/bin/env perl

use strict;
use warnings;

package Fixtures;
use Moo;
use lib 'lib';
with 'Interchange6::Test::Role::Fixtures';

has 'ic6s_schema' => ( is => 'ro', );

package main;
use Dancer2;
use Dancer2::Plugin::Interchange6;
use Fixtures;

set logger        => 'console';
set logger_format => '%m';

my $public = config->{'public_dir'};

my $schema = shop_schema;
$schema->deploy( { add_drop_table => 1 } );

my $fixtures = Fixtures->new( { ic6s_schema => $schema } );
$fixtures->load_all_fixtures;

my @navs = (
    {
        uri      => 'contact-us',
        type     => 'nav',
        scope    => 'top-left',
        name     => 'Contact Us',
        priority => 200,
        template => 'contact-us',
    },
    {
        uri      => 'orders',
        type     => 'nav',
        scope    => 'top-left',
        name     => 'Order Tracking',
        priority => 180,
    },
    {
        uri      => 'ship-to',
        type     => 'nav',
        scope    => 'top-right',
        name     => 'Ship To',
        priority => 100,
    },
    {
        uri      => 'language',
        type     => 'nav',
        scope    => 'top-right',
        name     => 'Language',
        priority => 200,
    },
    {
        uri => 'privacy',
        type => 'nav',
        scope => 'footer',
        name => 'Privacy Policy',
        priority => 200,
        template => 'privacy',
    },
    {
        uri => 'terms',
        type => 'nav',
        scope => 'footer',
        name => 'Terms and Conditions',
        priority => 180,
        template => 'terms',
    },
    {
        uri => 'returns',
        type => 'nav',
        scope => 'footer',
        name => 'Return Orders',
        priority => 160,
        template => 'privacy',
    },
    {
        uri => 'gift-certificates',
        type => 'nav',
        scope => 'footer',
        name => 'Gift Certificates',
        priority => 140,
        template => 'gift-certificates',
    },
);

# align media with actual images and add navigation
$schema->txn_do(
    sub {
        $fixtures->clear_media;

        my $imagetype =
          $schema->resultset('MediaType')->create( { type => 'image' } );

        my @brands = ('Mike Hammers', 'Racke Claws', 'Peter Kit', 'Claus Brush',
            'Ken Safty Clothes', 'Leo Measurement', 'Lica Ladder', 'SOS Planer',
            'Was Saw', 'Multitools');

        foreach my $brand ( @brands ){
            my $uri = lc($brand);
            $uri =~ s/ /-/g;

            push @navs, {
                uri   => 'brands/'. $uri,
                type  => 'nav',
                scope => 'brands',
                name  => $brand,
                priority => 140,
            }
        }

        foreach my $nav ( @navs ) {
            my $template = delete $nav->{template};
            my $result = shop_navigation->create($nav);
            $result->add_attribute('template', $template) if $template;
        }

        my $products = shop_product;
        while ( my $product = $products->next ) {
            my $sku = $product->canonical_sku || $product->sku;

            my $prod = $product->name;

            my $nav_prod;
            my ($brand_nav, $brand_name);

            if ($prod =~ /Hammer|Nail|Sledge|Mallet/){
                $brand_name = $brands[0];
            } elsif ($prod =~ /Claws/){
                $brand_name = $brands[1];
            } elsif ($prod =~ /Kit|Toolbox|Belt/){
                $brand_name = $brands[2];
            } elsif ($prod =~ /Roller|Brush|Paint/){
                $brand_name = $brands[3];
            }elsif ($prod =~ /Mask|Wear/) {
                $brand_name = $brands[4];
            }elsif( $prod =~ /Square|Ruler|Measure/){
                $brand_name = $brands[5];
            }elsif( $prod =~ /Ladder/){
                $brand_name = $brands[6];
            }elsif( $prod =~ /Planer/){
                $brand_name = $brands[7];
            }elsif( $prod =~ /Saw/){
                $brand_name = $brands[8];
            }
            else {
                $brand_name = $brands[9];
            }

            $brand_nav = shop_navigation->search(
                {
                    type  => 'nav',
                    scope => 'brands',
                    name  => $brand_name,
                },
                {
                    rows => 1,
                }
            )->next;


            $product->find_or_create_related( 'navigation_products',
                { navigation_id => $brand_nav->navigation_id } );

            my $img = "$sku.gif";

            my $path = path( $public, "img", "products", $img );

            if ( -r $path ) {
                $product->add_to_media(
                    {
                        file       => $path,
                        uri        => "/img/products/$img",
                        mime_type  => 'image/gif',
                        media_type => { type => 'image' },
                    }
                );
            }
            else {
                warning "Image $sku.gif not found or not readable at $path.\n";
            }
        }
    }
);
