#! /usr/bin/env perl

use strict;
use warnings;

use Dancer ':script';
use Dancer::Plugin::Interchange6;

set logger        => 'console';
set logger_format => '%m';

my $schema = shop_schema;

$schema->deploy( { add_drop_table => 1 } );

my $icecat   = shop_schema('icecat');
my $topcatid = '1314';

my @navs = (
    {
        uri      => 'contact-us',
        type     => 'nav',
        scope    => 'top-left',
        name     => 'Contact Us',
        priority => 200,
        template => 'contact-us',
    },
    {
        uri      => 'orders',
        type     => 'nav',
        scope    => 'top-left',
        name     => 'Order Tracking',
        priority => 180,
    },
    {
        uri      => 'ship-to',
        type     => 'nav',
        scope    => 'top-right',
        name     => 'Ship To',
        priority => 100,
    },
    {
        uri      => 'language',
        type     => 'nav',
        scope    => 'top-right',
        name     => 'Language',
        priority => 200,
    },
    {
        uri      => 'privacy',
        type     => 'nav',
        scope    => 'footer',
        name     => 'Privacy Policy',
        priority => 200,
        template => 'privacy',
    },
    {
        uri      => 'terms',
        type     => 'nav',
        scope    => 'footer',
        name     => 'Terms and Conditions',
        priority => 180,
        template => 'terms',
    },
    {
        uri      => 'returns',
        type     => 'nav',
        scope    => 'footer',
        name     => 'Return Orders',
        priority => 160,
        template => 'privacy',
    },
    {
        uri      => 'gift-certificates',
        type     => 'nav',
        scope    => 'footer',
        name     => 'Gift Certificates',
        priority => 140,
        template => 'gift-certificates',
    },
);

foreach my $nav (@navs) {
    my $template = delete $nav->{template};
    my $result   = shop_navigation->create($nav);
    $result->add_attribute( 'template', $template ) if $template;
}

my $media_type_image =
  shop_schema->resultset('MediaType')->create( { type => 'image' } );

my $media_type_video =
  shop_schema->resultset('MediaType')->create( { type => 'video' }, );

sub category_recurse {
    my ( $cat, $nav ) = @_;

    my $children = $cat->search_related(
        'children',
        {
            'names.langid'          => 1,
            'descriptions_2.langid' => 1,
        },
        {
            prefetch => [
                {
                    products =>
                      [ 'descriptions', 'features', 'galleries', 'supplier', ],
                },
                'names',
                'descriptions'
            ],
        },
    );

    while ( my $child = $children->next ) {
        my $products = $child->products;

        # drop empty categories
        next if ( $child->is_leaf && !$products->count );

        my $name = $child->names->first->value;

        ( my $uri = $name ) =~ s/\s+/-/g;
        if ($nav) {
            $uri = $nav->uri . "/$uri";
        }

        my $child_nav = shop_navigation->create(
            {
                uri         => $uri,
                type        => "nav",
                scope       => "menu-main",
                name        => $name,
                description => $child->descriptions->first->value,
                parent_id   => $nav ? $nav->id : undef,
            }
        );

        while ( my $product = $products->next ) {
            my $description = $product->descriptions->next;
            next unless $description;
            my $supplier = $product->supplier;

            # brand nav references all products from this supplier

            my $brand_nav = shop_navigation->search(
                {
                    type  => 'brand',
                    scope => 'brands',
                    name  => $supplier->name,
                },
                {
                    rows => 1,
                }
            )->next;

            if ( !$brand_nav ) {
                my $uri = lc( 'brand/' . $supplier->name );
                $uri =~ s/\s+/-/g;
                $brand_nav = shop_navigation->create(
                    {
                        uri   => $uri,
                        type  => 'brand',
                        scope => 'brands',
                        name  => $supplier->name
                    }
                );
            }

            # build sku from supplier name and product ID

            my $sku = lc( $supplier->name . '-' . $product->prod_id );
            $sku =~ s/\s+/-/g;

            my $ic6_product = shop_product->create(
                {
                    sku               => $sku,
                    name              => $product->name,
                    short_description => $description->short_desc,
                    description       => $description->long_desc,
                    price             => int( rand(10000) ) / 100 + 1,
                }
            );
            $ic6_product->create_related( 'navigation_products',
                { navigation_id => $child_nav->id } );
            $ic6_product->create_related( 'navigation_products',
                { navigation_id => $brand_nav->id } );

            if ( my $uri = $product->thumb_pic ) {
                $ic6_product->add_to_media(
                    {
                        uri            => $uri,
                        media_types_id => $media_type_image->id,
                        label          => "thumb",
                        priority       => 3000,
                    }
                );
            }

            if ( my $uri = $product->low_pic ) {
                $ic6_product->add_to_media(
                    {
                        uri            => $uri,
                        media_types_id => $media_type_image->id,
                        label          => "low",
                        priority       => 2000,
                    }
                );
            }

            if ( my $uri = $product->medium_pic ) {
                $ic6_product->add_to_media(
                    {
                        uri            => $uri,
                        media_types_id => $media_type_image->id,
                        label          => "medium",
                        priority       => 1000,
                    }
                );
            }

            if ( my $uri = $product->high_pic ) {
                $ic6_product->add_to_media(
                    {
                        uri            => $uri,
                        media_types_id => $media_type_image->id,
                        label          => "high",
                        priority       => 0,
                    }
                );
            }

            my $galleries = $product->galleries;
            my $i         = 0;
            while ( my $gallery = $galleries->next ) {
                $i++;
                if ( my $uri = $gallery->thumb_link ) {
                    $ic6_product->add_to_media(
                        {
                            priority       => 3000 + $i,
                            uri            => $uri,
                            media_types_id => $media_type_image->id,
                            label          => "thumb",
                        }
                    );
                }

                if ( my $uri = $gallery->low_link ) {
                    $ic6_product->add_to_media(
                        {
                            priority       => 2000 + $i,
                            uri            => $uri,
                            media_types_id => $media_type_image->id,
                            label          => "low",
                        }
                    );
                }

                if ( my $uri = $gallery->medium_link ) {
                    $ic6_product->add_to_media(
                        {
                            priority       => 1000 + $i,
                            uri            => $uri,
                            media_types_id => $media_type_image->id,
                            label          => "medium",
                        }
                    );
                }

                if ( my $uri = $gallery->link ) {
                    $ic6_product->add_to_media(
                        {
                            priority       => $i,
                            uri            => $uri,
                            media_types_id => $media_type_image->id,
                            label          => "high",
                        }
                    );
                }
            }
        }

        category_recurse( $child, $child_nav ) unless $child->is_leaf;
    }
}

my $topcat = $icecat->resultset('Category')->find($topcatid);

category_recurse($topcat);

# clean out empty leaf navs

my $nav_rset = shop_navigation;
my $navs     = $nav_rset->search(
    {
        type  => "nav",
        scope => "menu-main",
    }
)->with_active_product_count;

while ( my $nav = $navs->next ) {
    if ( $nav->is_leaf && !$nav->active_product_count ) {
        print "removing empty leaf nav: ", $nav->uri, "\n";
        $nav->delete;
    }
}
